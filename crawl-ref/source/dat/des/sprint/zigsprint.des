###############################################################################
# Zig Sprint
#
# Now using an actual ziggurat!
###############################################################################

: require("dlua/ziggurat.lua")

NAME:   sprint_v
TAGS:   sprint no_item_gen no_trap_gen no_pool_fixup no_rotate no_hmirror no_vmirror
DESC:   Sprint V: "Ziggurat Sprint"
ORIENT: encompass
LFLAGS: no_tele_control
: lfloorcol(util.random_from(
:   {"red", "blue", "green", "magenta", "brown", "lightgrey", "darkgrey"} ))
COLOUR:    x : lightred / lightblue / lightgreen / lightmagenta / yellow / darkgrey
TILE:      x : wall_zot_red / wall_zot_blue / wall_zot_green / wall_zot_magenta
SUBST:     x = X
KFEAT:  a = altar_zin
KFEAT:  b = altar_the_shining_one
KFEAT:  c = altar_ashenzari
KFEAT:  d = altar_kikubaaqudgha
KFEAT:  e = altar_yredelemnul
KFEAT:  f = altar_xom
KFEAT:  g = altar_vehumet
KFEAT:  h = altar_okawaru
KFEAT:  i = altar_makhleb
KFEAT:  j = altar_sif_muna
KFEAT:  k = altar_trog
KFEAT:  l = altar_nemelex_xobeh
KFEAT:  m = altar_elyvilon
KFEAT:  n = altar_lugonu
KFEAT:  o = altar_beogh
KFEAT:  p = altar_jiyva
KFEAT:  q = altar_fedhas
KFEAT:  r = altar_cheibriados
KFEAT:  A = enter_portal_vault
TILE:   A = dngn_portal_ziggurat
{{
    dgn.persist.ziggurat = { entry_fee = 5000 }
    initialise_ziggurat(dgn.persist.ziggurat)
    lua_marker('A', one_way_stair { desc = "gateway to a ziggurat",
                                    overview = "Ziggurat",
                                    dst = 'Zig:1', floor = 'stone_arch',
                                    feat_tile = "dngn_portal_ziggurat_gone" } )
}}
KITEM:    B = plate armour race:none mundane not_cursed ident:all plus:5, \
              fire dragon armour mundane not_cursed ident:all plus:5, \
              ice dragon armour mundane not_cursed ident:all plus:5, \
              storm dragon armour mundane not_cursed ident:all plus:5, \
              gold dragon armour mundane not_cursed ident:all plus:5, \
              crystal plate armour mundane not_cursed ident:all plus:5
KITEM:    C = buckler race:none mundane not_cursed ident:all plus:3, \
              shield race:none mundane not_cursed ident:all plus:3, \
              large race:none shield mundane not_cursed ident:all plus:3
KITEM:    D = broad axe race:none mundane not_cursed ident:all plus:5 plus2:5, \
              battleaxe race:none mundane not_cursed ident:all plus:5 plus2:5, \
              executioner's axe race:none mundane not_cursed ident:all plus:5 plus2:5
KITEM:    E = demon trident mundane not_cursed ident:all plus:5 plus2:5, \
              bardiche race:none mundane not_cursed ident:all plus:5 plus2:5
KITEM:    F = demon whip mundane not_cursed ident:all plus:5 plus2:5, \
              great mace race:none mundane not_cursed ident:all plus:5 plus2:5, \
              giant club mundane not_cursed ident:all plus:5 plus2:5, \
              giant spiked club mundane not_cursed ident:all plus:5 plus2:5
KITEM:    G = dagger race:none mundane not_cursed ident:all plus:5 plus2:5, \
              sabre race:none mundane not_cursed ident:all plus:5 plus2:5, \
              quick blade race:none mundane not_cursed ident:all plus:5 plus2:5
KITEM:    H = demon blade mundane not_cursed ident:all plus:5 plus2:5, \
              double sword race:none mundane not_cursed ident:all plus:5 plus2:5, \
              great sword race:none mundane not_cursed ident:all plus:5 plus2:5, \
              triple sword race:none mundane not_cursed ident:all plus:5 plus2:5
KITEM:    I = lajatang mundane not_cursed ident:all plus:5 plus2:5
KITEM:    J = crossbow race:none mundane not_cursed ident:all plus:5 plus2:5, \
              bolt q:1000 mundane ident:all, \
              bow race:none mundane not_cursed ident:all plus:5 plus2:5, \
          arrow q:1000 mundane ident:all, \
              longbow race:none mundane not_cursed ident:all plus:5 plus2:5, \
              sling mundane not_cursed ident:all plus:5 plus2:5, \
              sling bullet q:1000 mundane ident:all
KITEM:    K = potion of curing q:9 ident:all, \
              potion of heal wounds q:9 ident:all, \
              potion of berserk rage q:2 ident:all, \
              potion of might q:5 ident:all, \
              potion of agility q:5 ident:all, \
              potion of magic q:5 ident:all, \
              potion of experience q:1 ident:all, \
              potion of resistance q:4 ident:all, \
              potion of speed q:5 ident:all, \
              potion of restore abilities q:2 ident:all, \
              potion of invisibility q:2 ident:all, \
              potion of brilliance q:2 ident:all
KITEM:    L = scroll of blinking q:3 ident:all, \
              scroll of fear q:2 ident:all, \
              scroll of remove curse q:5 ident:all, \
              scroll of identify q:5 ident:all, \
              scroll of holy word q:1 ident:all, \
              scroll of fog q:7 ident:all, \
              scroll of recharging q:3 ident:all, \
          scroll of amnesia q:4 ident:all
KITEM:    M = meat ration q:4, bread ration q:4, strawberry q:42
KITEM:    N = wand of draining ident:all charges:5, \
              wand of cold ident:all charges:5, \
              wand of fire ident:all charges:5, \
          wand of disintegration ident:all charges:7
KITEM:    O = crystal ball of energy ident:all, staff of energy not_cursed ident:all
KITEM:    P = ring of strength not_cursed ident:all plus:6, \
              ring of strength not_cursed ident:all plus:6, \
              ring of intelligence not_cursed ident:all plus:6, \
              ring of intelligence not_cursed ident:all plus:6, \
              ring of dexterity not_cursed ident:all plus:6, \
              ring of dexterity not_cursed ident:all plus:6, \
          ring of flight not_cursed ident:all
KITEM:    R = broad axe unrand:obsidian_axe ident:all, \
              shield unrand:shield_of_the_gong ident:all, \
              plate armour unrand:maxwells_patent_armour ident:all
KITEM:    T = longbow unrand:piercer ident:all, \
              long sword unrand:singing_sword ident:all, \
              great mace unrand:mace_of_variability ident:all, \
              great sword unrand:sword_of_power ident:all, \
              scythe unrand:scythe_of_curses ident:all, \
              double sword unrand:sword_of_zonguldrok ident:all, \
              spear unrand:wyrmbane ident:all, \
              triple sword unrand:plutonium_sword ident:all, \
              club unrand:devastator ident:all
KITEM:    S = disc of storms ident:all, \
              tome of destruction ident:all, tome of destruction ident:all
KFEAT:    ^ = granite_statue
KFEAT:    & = granite_statue
COLOUR:   & = magenta
{{
  hook('post_main',
    function()
      if is_validating() then return end
      crawl.sprint_depth('Zig', 27)
    end
  )
}}
MAP
xxxxxxxxxxxxxxxxx
xf^g^h^&A&^i^j^kx
x^.........RT..^x
xe..HG.........lx
x^..IF.........^x
xd..JE.....S...mx
x^...D..M..P...^x
xc......L..O...nx
x^...C..K..N...^x
xb...B.........ox
x^.............^x
xa^r^.^^{^^.^q^px
xxxxxxxxxxxxxxxxx
ENDMAP

NAME: zigsprint_level
TAGS: sprint_level sprint_v
PLACE: Zig
: ziggurat_level(_G)
MAP
ENDMAP
